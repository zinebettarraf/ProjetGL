parser grammar DecaParser;

options {
    // Default language but name it anyway
    //
    language  = Java;

    // Use a superclass to implement all helper
    // methods, instance variables and overrides
    // of ANTLR default methods, such as error
    // handling.
    //
    superClass = AbstractDecaParser;

    // Use the vocabulary generated by the accompanying
    // lexer. Maven knows how to work out the relationship
    // between the lexer and parser and will build the
    // lexer before the parser. It will also rebuild the
    // parser if the lexer changes.
    //
    tokenVocab = DecaLexer;

}

// which packages should be imported?
@header {
    import fr.ensimag.deca.tree.*;
    import java.io.PrintStream;
    import fr.ensimag.deca.tools.SymbolTable;
    import fr.ensimag.deca.tools.SymbolTable.Symbol;
}

@members {
    @Override
    protected AbstractProgram parseProgram() {
        return prog().tree;
    }
}

prog returns[AbstractProgram tree]
    : list_classes main EOF {
            assert($list_classes.tree != null);
            assert($main.tree != null);
            $tree = new Program($list_classes.tree, $main.tree);
            setLocation($tree, $list_classes.start);
        }
    ;

main returns[AbstractMain tree]
    : /* epsilon */ {
            $tree = new EmptyMain();
        }
    | block {
            assert($block.decls != null);
            assert($block.insts != null);
            $tree = new Main($block.decls, $block.insts);
            setLocation($tree, $block.start);
        }
    ;

block returns[ListDeclVar decls, ListInst insts]
    : OBRACE list_decl list_inst CBRACE {
            assert($list_decl.tree != null);
            assert($list_inst.tree != null);
            $decls = $list_decl.tree;
            setLocation($decls, $list_decl.start);
            $insts = $list_inst.tree;
            setLocation($insts, $list_inst.start);
        }
    ;

list_decl returns[ListDeclVar tree]
@init   {
            $tree = new ListDeclVar();
        }
    : decl_var_set[$tree]*
    ;

decl_var_set[ListDeclVar l]
    : type list_decl_var[$l,$type.tree] SEMI
    ;

list_decl_var[ListDeclVar l, AbstractIdentifier t]
    : dv1=decl_var[$t] {
        $l.add($dv1.tree);
        } (COMMA dv2=decl_var[$t] {
            $l.add($dv2.tree);
        }
      )*
    ;

decl_var[AbstractIdentifier t] returns[AbstractDeclVar tree]
@init   {
        AbstractInitialization initializer;
        DeclVar elt;
        }
    : i=ident {
            AbstractIdentifier identifier = $i.tree;
            initializer = new NoInitialization();
            elt = new DeclVar($t, identifier, initializer);
            $tree = elt;
            setLocation ($tree, $i.start);
            setLocation (identifier, $i.start);
        }
      (EQUALS e=expr {
          initializer = new Initialization($e.tree);
          setLocation(initializer, $EQUALS);
          elt = new DeclVar($t, identifier, initializer);
          $tree = elt;
          setLocation($tree, $i.start);
        }
      )? {
          
        }
    ;

list_inst returns[ListInst tree]
@init {
       $tree=new ListInst();
}
    : (inst {
           $tree.add($inst.tree);
        }
      )*
    ;

inst returns[AbstractInst tree]
    : e1=expr SEMI {
            assert($e1.tree != null);
            $tree = $e1.tree;
            setLocation($tree, $e1.start);
        }
    | SEMI {
            $tree = new NoOperation();
            setLocation($tree, $SEMI);
        }
    | PRINT OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree=new Print(false, $list_expr.tree);
            setLocation($tree, $PRINT);
        }
    | PRINTLN OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree=new Println(false,$list_expr.tree);
            setLocation($tree, $PRINTLN);
        }
    | PRINTX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree=new Print(true,$list_expr.tree);
            setLocation($tree, $PRINTX);
        }
    | PRINTLNX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree=new Println(true,$list_expr.tree);
            setLocation($tree, $PRINTLNX);
        }
    | if_then_else {
            assert($if_then_else.tree != null);
            $tree = $if_then_else.tree;
            setLocation($tree, $if_then_else.start);
        }
    | WHILE OPARENT condition=expr CPARENT OBRACE body=list_inst CBRACE {
            assert($condition.tree != null);
            assert($body.tree != null);
            While newInst = new While($condition.tree, $body.tree);
            $tree = newInst;
            setLocation($tree, $WHILE);
        }
    | RETURN expr SEMI {
            assert($expr.tree != null);
            $tree = new Return($expr.tree);
            setLocation($tree, $RETURN);
        }
    ;

if_then_else returns[IfThenElse tree]
@init {
    IfThenElse lastInst;
    ListInst newList;
    ListInst lastList;
}
    : if1=IF OPARENT condition=expr CPARENT OBRACE li_if=list_inst CBRACE {
            newList = new ListInst();
            IfThenElse newInst1 = new IfThenElse($condition.tree, $li_if.tree, newList);
            $tree = newInst1;
            lastInst = newInst1;
            setLocation($tree, $if1);
            setLocation(newInst1, $if1);
        }
      (ELSE elsif=IF OPARENT elsif_cond=expr CPARENT OBRACE elsif_li=list_inst CBRACE {
            lastList = new ListInst();
            IfThenElse newInst2 = new IfThenElse($elsif_cond.tree, $elsif_li.tree, lastList);
            setLocation(newInst2, $if1);
            lastInst.addToElseBranch(newInst2);
            lastInst = newInst2;
        }
      )*
      (ELSE OBRACE li_else=list_inst CBRACE {
            lastInst.setElseBranch($li_else.tree);
        }
      )?
    ;

list_expr returns[ListExpr tree]
@init   {
        $tree= new ListExpr();
        }
    : (e1=expr {
        $tree.add($e1.tree);
        setLocation($tree, $e1.start);
        setLocation($e1.tree, $e1.start);
        }
       (COMMA e2=expr {
          $tree.add($e2.tree);
          setLocation($e2.tree, $e2.start);
        }
       )* )?
    ;

expr returns[AbstractExpr tree]
    : assign_expr {
            $tree = $assign_expr.tree;
            assert($assign_expr.tree != null);
            setLocation($tree, $assign_expr.start);
        }
    ;

assign_expr returns[AbstractExpr tree]
    : e=or_expr (
        /* condition: expression e must be a "LVALUE" */ {
            if (! ($e.tree instanceof AbstractLValue)) {
                throw new InvalidLValue(this, $ctx);
            } 
        }
        EQUALS e2=assign_expr {
            assert($e.tree != null);
            assert($e2.tree != null);
            $tree = new Assign ((AbstractLValue)$e.tree, $e2.tree);
            setLocation($tree, $e.start);
        }
      | /* epsilon */ {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
      )
    ;

or_expr returns[AbstractExpr tree]
    : e=and_expr {
            $tree=$e.tree;
            assert($e.tree != null);
            setLocation($tree, $e.start);
        }
    | e1=or_expr OR e2=and_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Or($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
       }
    ;

and_expr returns[AbstractExpr tree]
    : e=eq_neq_expr {
            $tree=$e.tree;
            assert($e.tree != null);
            setLocation($tree, $e.start);
        }
    |  e1=and_expr AND e2=eq_neq_expr {
            assert($e1.tree != null);                         
            assert($e2.tree != null);
            $tree = new And($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

eq_neq_expr returns[AbstractExpr tree]
    : e=inequality_expr {
            $tree=$e.tree;
            assert($e.tree != null);
            setLocation($tree, $e.start);
        }
    | e1=eq_neq_expr EQEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Equals($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=eq_neq_expr NEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new NotEquals($e1.tree, $e2.tree); 
            setLocation($tree, $e1.start);                                                          
        }
    ;

inequality_expr returns[AbstractExpr tree]
    : e=sum_expr {
            $tree=$e.tree;
            assert($e.tree != null);
            setLocation($tree, $e.start);
        }
    | e1=inequality_expr LEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new LowerOrEqual($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr GEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new GreaterOrEqual($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr GT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Greater($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr LT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Lower($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr INSTANCEOF type {
            assert($e1.tree != null);
            assert($type.tree != null);
            $tree = new InstanceOf($e1.tree, $type.tree);
            setLocation($tree, $e1.start);
        }
    ;


sum_expr returns[AbstractExpr tree]
    : e=mult_expr {
            $tree=$e.tree;
            assert($e.tree != null);
            setLocation($tree, $e.start);
        }
    | e1=sum_expr PLUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Plus($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=sum_expr MINUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Minus($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

mult_expr returns[AbstractExpr tree]
    : e=unary_expr {
            $tree=$e.tree;
            assert($e.tree != null);
            setLocation($tree, $e.start);
        }
    | e1=mult_expr TIMES e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Multiply($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=mult_expr SLASH e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Divide($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=mult_expr PERCENT e2=unary_expr {
            assert($e1.tree != null);                                                                          
            assert($e2.tree != null);
            $tree = new Modulo($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

unary_expr returns[AbstractExpr tree]
    : op=MINUS e=unary_expr {
            assert($e.tree != null);
            $tree = new UnaryMinus($e.tree);
            setLocation($tree, $op);
        }
    | op=EXCLAM e=unary_expr {
            assert($e.tree != null);
            $tree = new Not ($e.tree);
            setLocation($tree, $op);
        }
    | select_expr {
            $tree=$select_expr.tree;
            assert($select_expr.tree != null);
            setLocation($tree, $select_expr.start);
        }
    ;

select_expr returns[AbstractExpr tree]
    : e=primary_expr {
            assert($e.tree != null);
            $tree=$e.tree;
            setLocation($tree, $e.start);
        }
    | e1=select_expr DOT i=ident {
            assert($e1.tree != null);
            assert($i.tree != null);
            
        }
        (o=OPARENT args=list_expr CPARENT {
            // we matched "e1.i(args)"
            assert($args.tree != null);
            $tree = new MethodCall($e1.tree, $i.tree, $args.tree);
            setLocation($tree, $e1.start);
        } 
        | /* epsilon */ {
            // we matched "e.i"
            $tree = new Selection ($e1.tree, $i.tree);
            setLocation($tree, $e1.start);
        }
        )
    ;

primary_expr returns[AbstractExpr tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
            setLocation($tree, $ident.start);
        }
    | m=ident OPARENT args=list_expr CPARENT {
            assert($args.tree != null);
            assert($m.tree != null);
            $tree = new MethodCall(new This(), $m.tree, $args.tree);
            setLocation($tree, $m.start);
            
        }
    | OPARENT expr CPARENT {
            $tree = $expr.tree;
            assert($expr.tree != null);
            setLocation($tree, $OPARENT);
        }
    | READINT OPARENT CPARENT {
            $tree = new ReadInt();
            setLocation($tree, $READINT);
        }
    | READFLOAT OPARENT CPARENT {
            $tree = new ReadFloat();
            setLocation($tree, $READFLOAT);
        }
    | NEW ident OPARENT CPARENT {
            assert($ident.tree != null);
            $tree = new New($ident.tree);
            setLocation($tree, $NEW);
        }
    | cast=OPARENT type CPARENT OPARENT expr CPARENT {
            assert($type.tree != null);
            assert($expr.tree != null);
            $tree = new Cast($type.tree, $expr.tree);
            setLocation($tree, $cast);
        }
    | literal {
            $tree=$literal.tree;
            assert($literal.tree != null);
            setLocation($tree, $literal.start);
        }
    ;

type returns[AbstractIdentifier tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
            setLocation($tree, $ident.start);
        }
    ;

literal returns[AbstractExpr tree]
    : INT {
        try{
            IntLiteral elt=new IntLiteral(Integer.parseInt($INT.text));
            $tree=elt;
            setLocation($tree, $INT);
        }
        catch(NumberFormatException e){$tree = null; }
        }
    | fd=FLOAT {
         
        try{
            FloatLiteral elt=new FloatLiteral(Float.parseFloat($fd.text));
            $tree=elt;
            setLocation($tree, $fd);
        }
        catch(NumberFormatException e){$tree = null;} 
        }
        
    | STRING {
        String text = $STRING.text;
        //String newText = text.replace("\"", "");
        String newText = text.substring(1, (text.length() - 1));
        StringLiteral elt=new StringLiteral(newText);
        $tree=elt;
        setLocation($tree, $STRING);
        }
    | TRUE {
        BooleanLiteral elt = new BooleanLiteral(true);
        $tree = elt;
        setLocation($tree, $TRUE);
        }
    | FALSE {
        BooleanLiteral elt = new BooleanLiteral(false);
        $tree = elt;
        setLocation($tree, $FALSE);
        }
    | THIS {
            $tree = (AbstractExpr) new This();
            setLocation($tree, $THIS);
        }
    | NULL {
            $tree = (AbstractExpr) new Null();
            setLocation($tree, $NULL);
        }
    ;

ident returns[AbstractIdentifier tree]
    : IDENT {
            SymbolTable t = this.getDecacCompiler().getSymbolTable();
            Symbol newSymbol = t.create($IDENT.text);
            $tree = new Identifier(newSymbol);
            setLocation($tree, $IDENT);
        }
    ;

/****     Class related rules     ****/

list_classes returns[ListDeclClass tree]
@init { $tree = new ListDeclClass(); }
    :
      (c1=class_decl {
          $tree.add($c1.tree);
        }
      )*
    ;

class_decl returns[DeclClass tree]
    : CLASS name=ident superclass=class_extension OBRACE class_body CBRACE {
            $tree = new DeclClass($name.tree, $superclass.tree, $class_body.listDeclField, $class_body.listDeclMethod);
            setLocation($tree, $CLASS);
            setLocation($superclass.tree, $superclass.start);
        }
    ;

class_extension returns[AbstractIdentifier tree]
    : EXTENDS ident {
            $tree = $ident.tree;
            setLocation($tree, $EXTENDS);
        }
    | /* epsilon */ {
            SymbolTable t = this.getDecacCompiler().getSymbolTable();
            Symbol newSymbol = t.create("Object");
            $tree = new Identifier(newSymbol);
        }
    ;

class_body returns [ListDeclField listDeclField, ListDeclMethod listDeclMethod]
@init{
    $listDeclField = new ListDeclField();
    $listDeclMethod = new ListDeclMethod();
}

    : (m=decl_method {
            assert($m.tree != null);
            $listDeclMethod.add($m.tree);

        }
      | decl_field_set[$listDeclField]
      )*
    ;

decl_field_set[ListDeclField list]
    : v=visibility t=type list_decl_field[$list, $v.tree, $t.tree]
      SEMI
    ;

visibility returns[Visibility tree]
    : /* epsilon */ {
            $tree = Visibility.PUBLIC;
        }
    | PROTECTED {
            $tree = Visibility.PROTECTED;
        }
    ;

list_decl_field [ListDeclField list, Visibility v, AbstractIdentifier t]
    : dv1=decl_field [$list, $v, $t]{
            $list.add($dv1.tree);
            setLocation($list, $dv1.start);
        }
        (COMMA dv2=decl_field[$list, $v, $t]{
            $list.add($dv2.tree);
        }
      )*
    ;

decl_field[ListDeclField list, Visibility v, AbstractIdentifier t] returns [AbstractDeclField tree]
@init{
    DeclField elt;
    AbstractInitialization initializer;
}
    : i=ident {
            initializer = new NoInitialization();
            elt = new DeclField ($v, $t, $i.tree, initializer);
            $tree =elt;
            setLocation($tree, $i.start);
        }
      (EQUALS e=expr {
          initializer = new Initialization($e.tree);
          setLocation(initializer, $EQUALS);
          elt = new DeclField ($v, $t, $i.tree, initializer);
          $tree = elt;
          setLocation($tree, $i.start);
        }
      )? {
        }
    ;

decl_method returns [AbstractDeclMethod tree]
@init {
    DeclMethod elt;
    AbstractMethodBody methodBody;
}
    : type ident OPARENT params=list_params CPARENT (block {
            methodBody = new MethodBody ($block.decls, $block.insts);
            setLocation(methodBody, $block.start);
            assert($type.tree != null);
            assert($ident.tree != null);
            assert($params.tree != null);
            elt = new DeclMethod($type.tree, $ident.tree, $params.tree, methodBody);
            $tree = elt;
            setLocation($tree, $type.start);
        }
      | ASM OPARENT code=multi_line_string CPARENT SEMI {

            String newText = $code.text.substring(1, ($code.text.length() - 1));
            StringLiteral text = new StringLiteral(newText);
            methodBody = new MethodAsmBody(text);
            text.setLocation($code.location);
            setLocation(methodBody, $ASM);
            elt = new DeclMethod($type.tree, $ident.tree, $params.tree, methodBody);
            $tree = elt;
            setLocation($tree, $type.start);

        }
      ) {
        }
    ;

list_params returns[ListDeclParam tree]
@init{
        $tree = new ListDeclParam();
}
    : (p1=param {
            
            $tree.add($p1.tree);
            setLocation($tree, $p1.start);
        } (COMMA p2=param {
            $tree.add($p2.tree);
        }
      )*)?
    ;
    
multi_line_string returns[String text, Location location]
    : s=STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    | s=MULTI_LINE_STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    ;

param returns[AbstractDeclParam tree]
    : type ident {
            DeclParam elt = new DeclParam($type.tree, $ident.tree);
            $tree = elt;
            setLocation($tree, $type.start);
        }
    ;